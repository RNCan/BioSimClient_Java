/*
 *  Build file for gradle
 */


plugins {
    id 'com.palantir.git-version' version '3.0.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group = 'org'


java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(8))
  }
}

repositories {
    mavenCentral()
    mavenLocal()
}

println "Using java version " + JavaVersion.current()
println "Project directory " + project.projectDir
println "Build directory " + project.buildDir

sourceSets {
    main {    	
        java {
            srcDirs 'src'            
        }
        
        resources {
			srcDirs= ['src']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }

    test {
        java {
            srcDirs 'test'
        }
                
        resources {
			srcDirs= ['test']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }               
}

// make sure to set the compliance to Java 1.8 in the project properties if the Java version is later than 1.8
// make sure to set Java Compiler > Errors Warnings > Deprecated and Restricted API > Forbidden reference to WARNING instead of ERROR
dependencies {
	implementation 'com.cedarsoftware:json-io:4.13.0'
	testImplementation "com.fasterxml.jackson.core:jackson-databind:2.13.1"
    testImplementation "junit:junit:4.12"    
}

compileJava {
	project.version= gitVersion() 
	println "Building " + rootProject.name + " " + project.version
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    maxHeapSize = "2048m"
    
    exclude '**/ProductionEnvironmentTest.class'
}

task integrationTest(type: Test) {
	description = 'Runs the integration tests (unit tests run using the produced JAR file)'
    group = 'verification'
	dependsOn jar
	
	include '**/*.class'
	
	testLogging.showStandardStreams = true
    testClassesDirs = sourceSets.test.output.classesDirs
        
    // set classpath to test classes + test dependencies + JAR
    classpath = project.sourceSets.test.output + configurations.testRuntimeClasspath + files(jar.archiveFile) 
    outputs.upToDateWhen { false }
    mustRunAfter test
    maxHeapSize = "2048m"
}

check.dependsOn integrationTest

jar {
	manifest {
    	attributes("Built-By": "Mathieu Fortin",
        		   "Specification-Title": "Client for BioSIM Web API",
	       		   "Specification-Version": archiveVersion,                   
                   "Specification-Vendor": "Canadian Wood Fibre Centre",
                   "Implementation-Title": "biosimclient",
                   "Implementation-Vendor": "Canadian Wood Fibre Centre",
                   )
    }
    
	into ('/') {
		from files(['./README.txt', './COPYING.LESSER.txt'])			
	}
	
	// include all java files from main sourceset into the jar file (resources are already included)
	from sourceSets.main.allJava		
}

task fatJar(type: Jar) {
	group = 'build'
	description = 'Creates a fat jar for distribution'
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE		// to avoid duplicate files
    manifest {
    	attributes("Built-By": "Mathieu Fortin",
        		   "Specification-Title": "Client for BioSIM Web API",
	       		   "Specification-Version": archiveVersion,                   
                   "Specification-Vendor": "Canadian Wood Fibre Centre",
                   "Implementation-Title": "biosimclient",
                   "Implementation-Vendor": "Canadian Wood Fibre Centre",
                   )
    }
    archiveBaseName = 'biosimclient'
	destinationDirectory =  file("${installDirFatJar}")  
    from { sourceSets.main.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }		
}

task deleteInstallFolder(type: Delete) {
  delete "${installDir}"  
}

task install(type: Copy) {
	description = 'Copies the output jar to installation folder.'
	group = 'build'
	dependsOn jar,deleteInstallFolder,integrationTest
	from configurations.default
	from jar
	into "${installDir}"
}


 